// various features demo

// keep linefeeds
! local concat = newline

| array musics = jazz|pop|rock|country|western

> topic demo inherits global_commands

    + start
    - demo menu
    ^ type `list` for a list of stuff.

    + list
    - lets do the demos!
    ^ triggers and replies
    ^ topics
    ^ navigating triggers
    ^ arrays
    ^ capturing input to variables
    ^ inherits
    ^ simplified regex parser

    + you say *
    - I say <star>...♪♪ :saxophone: :notes:
    ^ {topic=music}{@start}

    + music
    - {topic=music}{@start}

    + *
    - hmm, not sure what you mean.

< topic



> topic music

    + start
    - you like jazz?

    + yes
    - cool, me too!
    ^ {@yesback}

    + yes i like jazz
    - xlnt
    ^ {@yesback}

    + (@yes)
    - aw right!
    ^ {@yesback}

    + (@no)
    - no? sour puss! {topic=askmusic}{@start}

    + yesback
    - <set likesjazz=true>

    + back
    - {topic=demo}{@start}

< topic

> topic askmusic

    + start
    - what kind of music do you like then?

    + (@musics)
    - you like <star>? cool.
    ^ <set favemusic=<star>>
    ^ {@back}

    + *
    - <star>? thats new.
    ^ <set favemusic=<star>>
    ^ {@back}

    + back
    - {topic=demo}{@start}

< topic
