// -----------------------------------------------------
// checkin_else.rive
// at a hotel
// -----------------------------------------------------

// ------------------------------------------------
! local concat = newline


// shared items for this lesson.
// note you can inherit multiple things here
// we put 'gambits' separately to organize the code

> topic checkin_shared inherits lessons_shared

  + vocab{weight=10}
  - some useful hotel related words:
  ^ reservation -
  ^ no smoking -
  ^ directions -
  ^ luggage -
  ^ wake up call

  + grammar{weight=10}
  - kbase: checkin
  // - this lesson is about expressing preference with 'prefer'
  //TODO - add link to lesson info page

  + manpic
  - type: image
  ^ media_id: #{hotel-hotelman}
  ^ who: hotelman

  + anything else{weight=10}
  - {topic=checkin_else}
  ^ goto: start

  + checkin resetall
  - resetall
  ^ <set checkin_wakeupcall=0>
  ^ <set checkin_restaurant=0>
  ^ <set checkin_luggage=0>
  ^ <set checkin_directions=0>

  + lessonhelp
  - you can review the lesson material here
  ^ goto: lesson kbase

  + lesson kbase
  - kbase: checkin
  ^ goto: endhelp

< topic


> topic checkin_else inherits checkin_shared

  // chance of gambit vs 'anything else'
  + start
  - Is there anything else I can help you with?
  ^ Say 'no' if you're finished.
  - {@checkin gambit}

  // some random gambit openings
  + checkin gambit
  * <get checkin_wakeupcall> == 0 => {topic=checkin_wakeupcall}{@start}
  * <get checkin_restaurant> == 0 => {topic=checkin_restaurant}{@start}
  * <get checkin_luggage> == 0 => {topic=checkin_luggage}{@start}
  * <get checkin_directions> == 0 => {topic=checkin_directions}{@start}
  - You seem to be fully checked in. {@next}

//  - Is that all?
//  - Let me know if you need anything else.
//  - Can I help with anything else?

  // catch all for exiting other threads, bounce back to 'anything else' with {@else}
  + else{weight=20}
  - {@anything else}
  ^ {topic=checkin_else}
  ^ goto: start

  // - {@voicehint}
  + voicehint
  - type: voice
  ^ media_id: #{hotel-anything-else}

  + hint1
  - {@checkin gambit}

  + hint2
  - Is there anything else? If not, just say "no"
  ^ who: hotelman

  + [*] directions [*]
  - {topic=checkin_directions}{@start}

  + [*] restaurant
  - {topic=checkin_directions}{@start}

  + [*] luggage [*]
  - {topic=checkin_luggage}{@start}

  + [*] sightseeing [*]
  - {topic=checkin_directions}{@start}

  + [*] internet [*]
  - All the rooms have full WiFi internet.

  + [*] (wake up call|wakeup call)
  - {topic=checkin_wakeupcall}{@start}

  + [*] gym [*]
  - I'm sorry but we don't have a gym {@scoreup gym 2}

  + [*] airport [*]
  - You can take a shuttle bus. Here are some directions. {@scoreup airport 2}

  + [*] breakfast [*]
  - Breakfast is served downstairs in the resturant from 7am until 10am {@scoreup breakfast 2}

  + [*] [swimming] pool [*]
  - There no swimming pool, I'm afraid. {@scoreup pool 2}

  + (@no)[thanks]
  @ next

  + (@yes)
  - I can help you with directions, local restaurants recommendations and more. We want you to have a nice stay here.
  ^ What's your question?

  + dunno
  - I'm sorry, I'm not sure about that.
  - I don't understand.
  - Hmm, I don't know.

  + next
  - {topic=checkin_end}
  ^ goto: start

  // CATCH ALL - this will stop other fallthrough
  // choose randomly. this * catch all will override anything in lessons_shared
  + *
  - {@hint2}

< topic


// -----------------------------------------------------
// 'else' topics also inherit checkin_else
// so they bounce to else if theres a wildcard caught here
// TODO each of these conversations could be expanded

> topic checkin_restaurant inherits checkin_else

  + start
  - would you like a local restaurant recommendation?
  ^ <set checkin_restaurant=1>

  + (@yes)
  @ about

  + (@no)
  - OK then. {@next}

  + about
  - i can recommend a nice french restaurant nearby. Here's their address.
  ^ {@next}

  + next
  - {@anything else}

< topic


// -----------------------------------------------------

> topic checkin_directions inherits checkin_else

  + start
  - Do you need local directions?
  ^ <set checkin_directions=1>

  + about
  - Let me get you a map! {@next}
  - This map will explain the area. {@next}
  - Here's a map of nearby attractions {@next}

  + (@yes) [*]
  @ about

  + (@no) [*]
  - You know your way around already! {@next}

  + next
  - {@anything else}

< topic



// -----------------------------------------------------

> topic checkin_wakeupcall inherits checkin_else

  + start
  - Do you need a wake up call?
  ^ <set checkin_wakeupcall=1>

  + about
  - You can set a wake up call from the phone in your room {@next}

  + hint1
  - Say you don't need one.
  ^ who: teacher1

  + i do not need (a wake up call|one)
  - understood. {@next}

  + (@yes) [*]
  - {@about}

  + (@no)[*]
  - very well {@next}

  + next
  - {@anything else}

< topic


// -----------------------------------------------------

> topic checkin_luggage inherits checkin_else

  + start
  - Do you have any luggage you would like us to take care of?
  ^ <set checkin_luggage=1>

  + about
  - If you have luggage {@yes}

  + hint1
  - did you bring anything?

  + (@yes) [*]
  - I'll have a valet look after it for you.
  ^ {@else}

  + (@no) [*]
  - No problem {@next}

  + next
  - {@anything else}

< topic



// -----------------------------------------------------

> topic checkin_end inherits checkin_shared

  // this will be 'said' in same block as last ending
  + start
  - Congratulations! You checked into the hotel!
  ^ <set checkin_score=<get lessonscore>>
  ^ who: teacher1
  ^ goto: endlesson

< topic
